import numpy as np

snakemake.utils.min_version("9.0.0")

# Define wrapper versions
wrapper_ver = "v6.0.1"

units = config["units"]
snakemake.utils.validate(
    units.drop(columns=["read_type_raw", "read_type_trim"]),
    schema=Path(workflow.current_basedir) / "schemas/units.schema.yaml",
)
config = config["cfg"]
snakemake.utils.validate(
    config, schema=Path(workflow.current_basedir) / "schemas/config.schema.yaml"
)


include: "rules/utils.smk"
include: "rules/extend.smk"
include: "rules/derep.smk"
include: "rules/repr_read.smk"
include: "rules/low_complex.smk"
include: "rules/nonpareil.smk"


##########################
### Read Dereplication ###
##########################
rule multiqc:
    input:
        ### Merge lanes
        expand_pd(
            rules.seqkit_stats.output.stats,
            units.assign(tool="merge_lanes", read_type_trim="collapsed"),
        ),
        expand_pd(
            rules.nonpareil_plot.output.json,
            units.assign(tool="merge_lanes", read_type_trim="collapsed"),
        ),
        ### Read extension
        expand_pd(
            rules.seqkit_stats.output.stats,
            units.assign(tool="extend/tadpole", read_type_trim="collapsed"),
        )
        if is_activated("extension")
        else [],
        expand_pd(
            rules.nonpareil_plot.output.json,
            units.assign(tool="extend/tadpole", read_type_trim="collapsed"),
        )
        if is_activated("extension")
        else [],
        ### Read derep
        expand_pd(
            rules.seqkit_stats.output.stats,
            units.assign(tool="derep", read_type_trim="collapsed"),
        )
        if is_activated("derep")
        else [],
        expand_pd(
            rules.nonpareil_plot.output.json,
            units.assign(tool="derep", read_type_trim="collapsed"),
        )
        if is_activated("derep")
        else [],
        ### Read grep
        expand_pd(
            rules.seqkit_stats.output.stats,
            units.assign(tool="represent/grep", read_type_trim="collapsed"),
        )
        if is_activated("extension") and is_activated("derep")
        else [],
        expand_pd(
            rules.nonpareil_plot.output.json,
            units.assign(tool="represent/grep", read_type_trim="collapsed"),
        )
        if is_activated("extension") and is_activated("derep")
        else [],
        ### Low complexity filter
        expand_pd(
            rules.seqkit_stats.output.stats,
            units.assign(tool="low_complexity", read_type_trim="collapsed"),
        ),
        expand_pd(
            rules.nonpareil_plot.output.json,
            units.assign(tool="low_complexity", read_type_trim="collapsed"),
        ),
    output:
        html="reports/multiqc.html",
        data="reports/multiqc_data.zip",
    log:
        "logs/reports/multiqc.log",
    benchmark:
        "benchmarks/reports/multiqc.log"
    params:
        extra="--force --clean-up " + config.get("report", {}).get("multiqc", ""),
    localrule: True
    threads: 1
    resources:
        mem=lambda w, attempt: f"{10* attempt} GiB",
        runtime=lambda w, attempt: f"{30* attempt} m",
    wrapper:
        f"{wrapper_ver}/bio/multiqc"


rule all:
    input:
        qc=rules.multiqc.output.html,
        res=expand_pd(
            expand(
                rules.low_complexity.output.out,
                read_type_trim="collapsed",
                allow_missing=True,
            ),
            units,
        ),
    message:
        "Read DeReping/Extension finished successfully!"
    default_target: True
